System conwaycellsnqakaive
mqttBroker "192.168.1.132" : 1883 eventTopic "lifeevents" 

Event startthegame     : startthegame(X)
Event    stopthecell   : stopthecell(X) 
Event    synch         : synch(X)
Event    clearCell     : clearCell(X)      
Event curstate         : curstate(NB,STATE)

Event kernel_rawmsg    : kernel_rawmsg(ARG)

Dispatch changeCellState   : changeCellState(X,Y)
Dispatch allnbreceived     : allnbreceived(N)

Context ctxcells ip [ host="localhost" port=8360] 


QActor cellbuilder context ctxcells{
import "unibo.basicomm23.mqtt.*"

 State s0 initial{
 	println("$name | STARTS")
 }
 Goto work
 
 
 State work{
 	println("$name | WORK")
 	[# createActorDynamically( "cell", "_0_0", false) 
 	   createActorDynamically( "cell", "_0_1", false)
 	   createActorDynamically( "cell", "_1_0", false)	
 	   createActorDynamically( "cell", "_1_1", false)	
 	#]
 }
}

 
/*
 * ------------------------------------------
 * cell
 * ------------------------------------------
 */
QActor cell context ctxcells dynamicOnly{
import "main.java.*"
	[#
	   var MyState     = false
	   var Countnb     = 0  //numero dei vicini
	   var Countnbon   = 0  //numero degli stati on dei vicini
	   var Countnbmsgs = 0  //numero dei msg ricevuti dai vicini	  
	   var  X          = 0
	   var  Y          = 0
	   var XD          = 0
	   var YD          = 0
	   
	   lateinit  var outindev  : OutInCellGui
	   val guiinterpreter = main.java.CellCmdTranslator( myself )  //ADDED
 		 
 fun setCellCoords( )  {
     val coords = name.replace("cell_","").split("_")  //Before cellc
     X = coords[0].toInt()
     XD = X+1
     Y  = coords[1].toInt()        
     YD =Y+1
  }
  
  fun displayOnGui(){ 
	val V   = if (MyState) 1 else 0
	val msg = "cell($XD,$YD,$V)" 
	outindev.display(msg)   	
  }
        
	#]
	State s0 initial{
		delay 3000 // connessione MQTT ....
		[# setCellCoords( ) #] 
		println("$name | START X=$X Y=$Y ") color green
		//println("$name | STARTSSSSSSSSSSSSSS") color blue
		[#  //setCellCoords( )
			Countnb = conway.GridSupport25.subscribeToNeighborsMqtt(myself, X,Y)
		#]       	
		println("$name | STARTS Countnb= $Countnb") color black
		
 		[# outindev = OutInCellGui(myself, "guiin") #]

    }  
	Transition t0  
		whenEvent clearCell       -> clearThecell
		whenMsg changeCellState   -> changeCellState
 		whenEvent startthegame    -> emitinfophase


	State changeCellState{
		onMsg( changeCellState : changeCellState(X,Y) ){
			if [# X == payloadArg(0).toInt() && Y == payloadArg(1).toInt() #]{ //RIGUARDA LA CELLA
				[#  MyState = ! MyState;
					displayOnGui()
				#]
				println("$name | changeCellState=$MyState ") color yellow
			 }
		}   
	}
	Transition t0  
  		whenEvent clearCell       -> clearThecell
		whenMsg changeCellState   -> changeCellState
		whenEvent startthegame    -> emitinfophase
  	 
	State emitinfophase{
 		[# var MyInfo = "$name,$MyState" #]
 		println("$name | emitinfophase") color green
//	    delay 10 //cede il controllo  per rescheduling ....
        [# emitstreammqtt(name, "curstate","curstate($MyInfo)") #]
 	}
	Transition t0   
		whenEvent curstate    -> elabinfophase
 	    whenEvent stopthecell   -> stopthecell
				  
    State elabinfophase{
  		[# Countnbmsgs=Countnbmsgs+1 #]
 		println("$name | elabinfophase $Countnbmsgs") color blue
		onMsg ( curstate : curstate(NB,true) ){
			[# Countnbon = Countnbon + 1  #]
		} 
		if [# Countnbmsgs == Countnb #] {  		 
			autodispatch allnbreceived : allnbreceived(ok)
		}		  
    } 
	Transition t0 
		  whenEvent curstate    -> elabinfophase
		  whenMsg allnbreceived -> elabstatephase
 	      whenEvent stopthecell   -> stopthecell
	
	State elabstatephase{
 		println("$name | elabstatephase $MyState $Countnbon") color yellow
		if [# MyState  #]{ //soprevvice o muore
			[# MyState = Countnbon==2 || Countnbon==3 #]
		}
		if [# ! MyState  #]{ //rinasce o rimane morta
			[# MyState = (Countnbon==3) #]
		}
		//Aggiorno il display 	 		
		[# Countnbmsgs = 0
		   Countnbon   = 0 
           displayOnGui() 
        #]
  	}
	Transition t0 
		whenTime 1000           -> emitinfophase  //(no synch)
 	    whenEvent stopthecell   -> stopthecell
        whenEvent clearCell     -> clearThecell 
 
	State stopthecell{
		println("$name | stopthecell ") color magenta 
	}
    Transition t0  
    	whenMsg changeCellState -> changeCellState
        whenEvent startthegame  -> emitinfophase 
        whenEvent clearCell     -> clearThecell 
 
    State clearThecell{
    	//println("$name | clearThecell ")
     	[# MyState = false #]
     	[# displayOnGui() #] 
     }
    Transition t0  
    	whenMsg changeCellState -> changeCellState
        whenEvent startthegame  -> emitinfophase 
        whenEvent clearCell     -> clearThecell 
  	    whenEvent stopthecell   -> stopthecell
 
 
 
}//cell 
